module RegisterFile #(
	parameter					WIDTH = 32,		//ancho
	parameter					DEPTH = 5		//profundidad
)
(
	//Señales generales
	input							clk_i,
	//1er Puerto de lectura - Puerto A
	input							rdena_i,		//Habilitación de lectura
	input			[DEPTH-1:0]	addrda_i,	//Dirección
	output		[WIDTH-1:0]	datarda_o,	//Dato
	//2do Puerto de lectura - Puerto B
	input							rdenb_i,		//Habilitación de lectura
	input			[DEPTH-1:0]	addrdb_i,	//Dirección
	output		[WIDTH-1:0]	datardb_o,	//Dato
	//Puerto de escritura
	input							wren_i,		//Habilitación de escritura
	input			[DEPTH-1:0]	addwr_i,		//Dirección
	input			[WIDTH-1:0]	datawr_i		//Dato
);

	//Definición de la memoria
	reg			[WIDTH-1:0]	memoria	[(2**DEPTH)-1:0];

	//Puerto de escritura
	always @(posedge clk_i)
	begin
		if (wren_i)
			begin
				memoria[addwr_i] <= datawr_i;
			end
	end
	
	//1er Puerto de lectura - Puerto A
	assign		datarda_o	=	(rdena_i) ? memoria[addrda_i] : {WIDTH{1'b0}};
	
	//2do Puerto de lectura - Puerto B
	assign		datardb_o	=	(rdenb_i) ? memoria[addrdb_i] : {WIDTH{1'b0}};
	
endmodule 